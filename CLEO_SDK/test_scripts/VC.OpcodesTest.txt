{$CLEO .cs}

0000:
const
POSX       = 0@
PLAYER_CAR = 0@
RAMPAGE    = 0@
POSY       = 1@
PHONE_A    = 1@
POSZ       = 2@
PHONE_B    = 2@
SPAWN_CHAR = 3@
SPAWN_CAR  = 4@
LINE       = 5@
AIM_KEY    = 6@
ARMY_REQ   = 7@
FBI_REQ    = 8@
SWAT_REQ   = 9@
FREEBOMB   = 10@
SETTAXI    = 11@
MAXWANTED  = 12@
SPAWN_OBJ  = 13@
DRUNK      = 14@
SEATS      = 15@
end

while true
    wait 0
    if and
        00E1:   player 0 pressed_button 4 // action
        80E0:   not player $PLAYER_CHAR in_any_car
    then
        break
    end
end

while true
    wait 0
    if
        80E1:   not player 0 pressed_button 4
    then
        break
    end
end

0109: player $PLAYER_CHAR money += 10000
0054: store_player $PLAYER_CHAR position_to POSX POSY POSZ
0247: request_model #COLT45
0247: request_model #MALE01
0247: request_model #DINGHY
038B: load_requested_models
POSX += 4.0
009A: SPAWN_CHAR = create_actor_pedtype 4 model #MALE01 at POSX POSY POSZ
POSX += 4.0
00A5: SPAWN_CAR = create_car #DINGHY at POSX POSY POSZ
0229: set_car SPAWN_CAR color_to 6 75
POSX += 4.0
029B: SPAWN_OBJ = init_object #ELECTRICGATE at POSX POSY POSZ
0249: release_model #COLT45
0249: release_model #DINGHY
01B1: give_player $PLAYER_CHAR weapon 17 ammo 10
024A: PHONE_A = get_phone_at 38.341 -1023.3
024A: PHONE_B = get_phone_at 37.62793 -1023.3
FREEBOMB = 0
SETTAXI = 0
SWAT_REQ = 0
FBI_REQ = 0
ARMY_REQ = 0

while true
    wait 10
    03F0: enable_text_draw 0
    LINE = 0
    gosub @IS_CHAR_STILL_ALIVE
    gosub @IS_CAR_STILL_ALIVE
    gosub @RETURN_TRUE
    gosub @RETURN_FALSE
    gosub @GET_PAD_STATE
    gosub @ADD_AMMO_TO_PLAYER
    gosub @IS_PLAYER_STILL_ALIVE
    gosub @HAS_PLAYER_BEEN_ARRESTED
    gosub @CHANGE_CAR_LOCK
    gosub @SHAKE_CAM_WITH_POINT
    gosub @IS_CAR_IN_AIR
    gosub @IS_PLAYER_TOUCHING_OBJECT
    gosub @IS_CHAR_TOUCHING_OBJECT
    gosub @SET_CHAR_AMMO
    gosub @SET_FREE_BOMBS
    gosub @SET_ALL_TAXI_LIGHTS
    gosub @IS_CAR_ARMED_WITH_ANY_BOMB
    gosub @IS_CAR_ARMED_WITH_BOMB
    gosub @IS_PLAYER_TOUCHING_OBJECT_ON_FOOT
    gosub @IS_CHAR_TOUCHING_OBJECT_ON_FOOT
    gosub @ARM_CAR_WITH_BOMB
    gosub @SET_REPEATED_PHONE_MESSAGE
    gosub @SET_PHONE_MESSAGE
    gosub @IS_BOAT
    gosub @SET_SWAT_REQUIRED
    gosub @SET_FBI_REQUIRED
    gosub @SET_ARMY_REQUIRED
    gosub @IS_CHAR_STOPPED
    gosub @DROP_MINE
    gosub @DROP_NAUTICAL_MINE
    gosub @IS_FIRST_CAR_COLOUR
    gosub @IS_SECOND_CAR_COLOUR
    gosub @START_KILL_FRENZY_HEADSHOT
    gosub @SET_2_REPEATED_PHONE_MESSAGES
    gosub @SET_2_PHONE_MESSAGES
    gosub @SET_3_REPEATED_PHONE_MESSAGES
    gosub @SET_3_PHONE_MESSAGES
    gosub @SET_4_REPEATED_PHONE_MESSAGES
    gosub @SET_4_PHONE_MESSAGES
    gosub @SET_5_REPEATED_PHONE_MESSAGES
    gosub @SET_5_PHONE_MESSAGES
    gosub @SET_6_REPEATED_PHONE_MESSAGES
    gosub @SET_6_PHONE_MESSAGES
    gosub @IS_COLLISION_IN_MEMORY
    gosub @IS_CAR_VISIBLY_DAMAGED
    gosub @SET_GET_OUT_OF_JAIL_FREE
    gosub @IS_PLAYER_LIFTING_A_PHONE
    gosub @GET_NUMBER_OF_SEATS_IN_MODEL
    gosub @IS_CHAR_IN_ANY_BOAT
    gosub @IS_CHAR_IN_ANY_HELI
    gosub @IS_CHAR_IN_ANY_PLANE
    gosub @IS_CHAR_IN_FLYING_VEHICLE
    gosub @GET_MAX_WANTED_LEVEL
    gosub @GET_PLAYER_DRUNKENNESS
    gosub @ADD_LOAN_SHARK_VISITS
    gosub @ADD_MOVIE_STUNTS
    gosub @ADD_GARBAGE_PICKUPS
    gosub @SET_TOP_SHOOTING_RANGE_SCORE
    gosub @ADD_SHOOTING_RANGE_RANK
    gosub @IS_CHAR_TOUCHING_VEHICLE

    if and
        00E1:   player 0 pressed_button 4 // action
        80E0:   not player $PLAYER_CHAR in_any_car
    then
        01C2: mark_actor_as_no_longer_needed SPAWN_CHAR
        01C3: mark_car_as_no_longer_needed SPAWN_CAR
        01C4: mark_object_as_no_longer_needed SPAWN_OBJ
        05DC: end_custom_thread
    end
    if
        8447:   not player $PLAYER_CHAR lifting_a_phone
    then
        0308: text_6numbers 'HJSTATW' AIM_KEY MAXWANTED DRUNK SEATS 0 0 time 10 flag 5
    end
end

:TEXT_STYLE
0343: set_text_linewidth 500.0
033F: set_text_draw_letter_width_height 0.3 1.2
LINE += 11.0
return

:IS_CHAR_STILL_ALIVE
gosub @TEXT_STYLE
if
    00A2:   actor SPAWN_CHAR alive
then
    0608: show_text_position 10.0 LINE text "00A2: Spawned character is still alive"
else
    0608: show_text_position 10.0 LINE text "00A2: Spawned character is dead"
end
return

:IS_CAR_STILL_ALIVE
gosub @TEXT_STYLE
if
    00AC:   car SPAWN_CAR alive
then
    0608: show_text_position 10.0 LINE text "00AC: Spawned car is still alive"
else
    0608: show_text_position 10.0 LINE text "00AC: Spawned car is dead"
end
return

:RETURN_TRUE
gosub @TEXT_STYLE
if
    00C5:   true
then
    0608: show_text_position 10.0 LINE text "00C5: Returned true"
else
    0608: show_text_position 10.0 LINE text "00C5: Returned false"
end
return

:RETURN_FALSE
gosub @TEXT_STYLE
if
    00C6:   false
then
    0608: show_text_position 10.0 LINE text "00C6: Returned true"
else
    0608: show_text_position 10.0 LINE text "00C6: Returned false"
end
return

:GET_PAD_STATE
00E2: get_player 0 key 6 state_to AIM_KEY
return

:ADD_AMMO_TO_PLAYER
if
    0ADC: test_cheat "ADDAMMO"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0113: add_ammo_to_player $PLAYER_CHAR weapon 17 ammo 10
end
return

:IS_PLAYER_STILL_ALIVE
gosub @TEXT_STYLE
if
    0116:   player $PLAYER_CHAR alive
then
    0608: show_text_position 10.0 LINE text "0116: Player is alive"
else
    0608: show_text_position 10.0 LINE text "0116: Player is dead"
end
return

:HAS_PLAYER_BEEN_ARRESTED
gosub @TEXT_STYLE
if
    0130:   player $PLAYER_CHAR busted
then
    0608: show_text_position 10.0 LINE text "0130: Player has been arrested"
else
    0608: show_text_position 10.0 LINE text "0130: Player has not been arrested"
end
return

:CHANGE_CAR_LOCK
if
    00E0:   player $PLAYER_CHAR in_any_car
then
    if
        0ADC: test_cheat "LOCK"
    then
        018C: play_sound 1 at 0.0 0.0 0.0
        03C1: PLAYER_CAR = player $PLAYER_CHAR car_no_save
        0135: change_car PLAYER_CAR lock 2
    end
end
return

:SHAKE_CAM_WITH_POINT
if
    0ADC: test_cheat "SHAKE"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0054: store_player $PLAYER_CHAR position_to POSX POSY POSZ
    POSX += 100.0
    0136: shake_camera 5000 with_point POSX POSY POSZ
end
return

:IS_CAR_IN_AIR
gosub @TEXT_STYLE
if
    00E0:   player $PLAYER_CHAR in_any_car
then
    03C1: PLAYER_CAR = player $PLAYER_CHAR car_no_save
    if
        015E:   car PLAYER_CAR in_air
    then
        0608: show_text_position 10.0 LINE text "015E: Car is in air"
        return
    end
end
0608: show_text_position 10.0 LINE text "015E: Car is not in air"
return

:IS_PLAYER_TOUCHING_OBJECT
gosub @TEXT_STYLE
if
    0178:   player $PLAYER_CHAR touching_object SPAWN_OBJ
then
    0608: show_text_position 10.0 LINE text "0178: Player is touching spawned object"
else
    0608: show_text_position 10.0 LINE text "0178: Player is not touching spawned object"
end
return

:IS_CHAR_TOUCHING_OBJECT
gosub @TEXT_STYLE
if
    0179:   actor $PLAYER_ACTOR touching_object SPAWN_OBJ
then
    0608: show_text_position 10.0 LINE text "0179: Player character is touching spawned object"
else
    0608: show_text_position 10.0 LINE text "0179: Player character is not touching spawned object"
end
return

:SET_CHAR_AMMO
if
    0ADC: test_cheat "SETAMMO"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    017B: set_actor $PLAYER_ACTOR weapon 17 ammo_to 100
end
return

:SET_FREE_BOMBS
if
    0ADC: test_cheat "FREEBOMBS"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    if
        FREEBOMB == 0
    then
        FREEBOMB = 1
        021D: set_free_bomb_shop_to 1
    else
        FREEBOMB = 0
        021D: set_free_bomb_shop_to 0
    end
end
return

:SET_ALL_TAXI_LIGHTS
if
    0ADC: test_cheat "TAXI"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    if
        SETTAXI == 0
    then
        SETTAXI = 1
        021F: set_all_taxi_lights 1
    else
        SETTAXI = 0
        021F: set_all_taxi_lights 0
    end
end
return

:IS_CAR_ARMED_WITH_ANY_BOMB
gosub @TEXT_STYLE
if
    00E0:   player $PLAYER_CHAR in_any_car
then
    03C1: PLAYER_CAR = player $PLAYER_CHAR car_no_save
    if
        0220:   car PLAYER_CAR has_car_bomb
    then
        0608: show_text_position 10.0 LINE text "0220: Car is armed with any bomb"
        return
    end
end
0608: show_text_position 10.0 LINE text "0220: Car is not armed with any bomb"
return

:IS_CAR_ARMED_WITH_BOMB
gosub @TEXT_STYLE
if
    00E0:   player $PLAYER_CHAR in_any_car
then
    03C1: PLAYER_CAR = player $PLAYER_CHAR car_no_save
    if
        0228:   car PLAYER_CAR bomb_status == 4
    then
        0608: show_text_position 10.0 LINE text "0228: Car is armed with bomb 4"
        return
    end
end
0608: show_text_position 10.0 LINE text "0228: Car is not armed with bomb 4"
return

:IS_PLAYER_TOUCHING_OBJECT_ON_FOOT
gosub @TEXT_STYLE
if
    023A:   player $PLAYER_CHAR touching_object SPAWN_OBJ on_foot
then
    0608: show_text_position 10.0 LINE text "023A: Player is touching spawned object on foot"
else
    0608: show_text_position 10.0 LINE text "023A: Player is not touching spawned object on foot"
end
return

:IS_CHAR_TOUCHING_OBJECT_ON_FOOT
gosub @TEXT_STYLE
if
    023B:   actor $PLAYER_ACTOR touching_object SPAWN_OBJ on_foot
then
    0608: show_text_position 10.0 LINE text "023B: Player character is touching spawned object on foot"
else
    0608: show_text_position 10.0 LINE text "023B: Player character is not touching spawned object on foot"
end
return

:ARM_CAR_WITH_BOMB
if
    00E0:   player $PLAYER_CHAR in_any_car
then
    if
        0ADC: test_cheat "ARMBOMB"
    then
        018C: play_sound 1 at 0.0 0.0 0.0
        03C1: PLAYER_CAR = player $PLAYER_CHAR car_no_save
        0242: set_car PLAYER_CAR bomb_status_to 1
    end
end
return

:SET_REPEATED_PHONE_MESSAGE
if
    0ADC: test_cheat "PHONEA1X"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    024B: set_phone PHONE_A message_repeated 'ADMIRAL'
end
if
    0ADC: test_cheat "PHONEB1X"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    024B: set_phone PHONE_B message_repeated 'BENSON'
end
return

:SET_PHONE_MESSAGE
if
    0ADC: test_cheat "PHONEA1Y"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    024C: set_phone PHONE_A message 'ADMIRAL'
end
if
    0ADC: test_cheat "PHONEB1Y"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    024C: set_phone PHONE_B message 'BENSON'
end
return

:IS_BOAT
gosub @TEXT_STYLE
if
    00E0:   player $PLAYER_CHAR in_any_car
then
    03C1: PLAYER_CAR = player $PLAYER_CHAR car_no_save
    if
        029C:   is PLAYER_CAR boat
    then
        0608: show_text_position 10.0 LINE text "029C: Is a boat"
        return
    end
end
0608: show_text_position 10.0 LINE text "029C: Is not a boat"
return

:SET_SWAT_REQUIRED
if
    0ADC: test_cheat "SWATREQ"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    if
        SWAT_REQ == 0
    then
        SWAT_REQ = 1
        02BC: set_swat_required 1
    else
        SWAT_REQ = 0
        02BC: set_swat_required 0
    end
end
return

:SET_FBI_REQUIRED
if
    0ADC: test_cheat "FBIREQ"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    if
        FBI_REQ == 0
    then
        FBI_REQ = 1
        02BD: set_fbi_required 1
    else
        FBI_REQ = 0
        02BD: set_fbi_required 0
    end
end
return

:SET_ARMY_REQUIRED
if
    0ADC: test_cheat "ARMYREQ"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    if
        ARMY_REQ == 0
    then
        ARMY_REQ = 1
        02BE: set_army_required 1
    else
        ARMY_REQ = 0
        02BE: set_army_required 0
    end
end
return

:IS_CHAR_STOPPED
gosub @TEXT_STYLE
if
    02A0:   actor $PLAYER_ACTOR stopped
then
    0608: show_text_position 10.0 LINE text "02A0: Player character is 'stopped'"
else
    0608: show_text_position 10.0 LINE text "02A0: Player character is not 'stopped'"
end
return

:DROP_MINE
if
    0ADC: test_cheat "MINEA"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0054: store_player $PLAYER_CHAR position_to POSX POSY POSZ
    02F0: create_explosive_barrel_at POSX POSY -100.0
end
return

:DROP_NAUTICAL_MINE
if
    0ADC: test_cheat "MINEB"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0054: store_player $PLAYER_CHAR position_to POSX POSY POSZ
    02F1: create_explosive_barrel_at POSX POSY -100.0
end
return

:IS_FIRST_CAR_COLOUR
gosub @TEXT_STYLE
if
    00E0:   player $PLAYER_CHAR in_any_car
then
    03C1: PLAYER_CAR = player $PLAYER_CHAR car_no_save
    if
        031B:   car PLAYER_CAR primary_color 6
    then
        0608: show_text_position 10.0 LINE text "031B: Car primary colour is 6"
        return
    end
end
0608: show_text_position 10.0 LINE text "031B: Car primary colour is not 6"
return

:IS_SECOND_CAR_COLOUR
gosub @TEXT_STYLE
if
    00E0:   player $PLAYER_CHAR in_any_car
then
    03C1: PLAYER_CAR = player $PLAYER_CHAR car_no_save
    if
        031C:   car PLAYER_CAR secondary_color 75
    then
        0608: show_text_position 10.0 LINE text "031C: Car secondary colour is 75"
        return
    end
end
0608: show_text_position 10.0 LINE text "031C: Car secondary colour is not 75"
return

:START_KILL_FRENZY_HEADSHOT
01FA: RAMPAGE = rampage_status
if and
    0ADC: test_cheat "RAMPAGE"
    not RAMPAGE == 1
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0247: request_model #M4
    038B: load_requested_models
    0367: init_headshot_rampage 'RAMPAGE' weapon 26 time 120000 targets 10 -1 -1 -1 -1 0
    0249: release_model #M4
end
return

:SET_2_REPEATED_PHONE_MESSAGES
if
    0ADC: test_cheat "PHONEA2X"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0378: set_phone PHONE_A 2messages_repeated 'ADMIRAL' 'AMBULAN'
end
if
    0ADC: test_cheat "PHONEB2X"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0378: set_phone PHONE_B 2messages_repeated 'BENSON' 'BFINJC'
end
return

:SET_2_PHONE_MESSAGES
if
    0ADC: test_cheat "PHONEA2Y"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0379: set_phone PHONE_A 2messages 'ADMIRAL' 'AMBULAN'
end
if
    0ADC: test_cheat "PHONEB2Y"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0379: set_phone PHONE_B 2messages 'BENSON' 'BFINJC'
end
return

:SET_3_REPEATED_PHONE_MESSAGES
if
    0ADC: test_cheat "PHONEA3X"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    037A: set_phone PHONE_A 3messages_repeated 'ADMIRAL' 'AMBULAN' 'ANGEL'
end
if
    0ADC: test_cheat "PHONEB3X"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    037A: set_phone PHONE_B 3messages_repeated 'BENSON' 'BFINJC' 'BLISTAC'
end
return

:SET_3_PHONE_MESSAGES
if
    0ADC: test_cheat "PHONEA3Y"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    037B: set_phone PHONE_A 3messages 'ADMIRAL' 'AMBULAN' 'ANGEL'
end
if
    0ADC: test_cheat "PHONEB3Y"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    037B: set_phone PHONE_B 3messages 'BENSON' 'BFINJC' 'BLISTAC'
end
return

:SET_4_REPEATED_PHONE_MESSAGES
if
    0ADC: test_cheat "PHONEA4X"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    037C: set_phone PHONE_A 4messages_repeated 'ADMIRAL' 'AMBULAN' 'ANGEL' 'BAGGAGE'
end
if
    0ADC: test_cheat "PHONEB4X"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    037C: set_phone PHONE_B 4messages_repeated 'BENSON' 'BFINJC' 'BLISTAC' 'BLOODRA'
end
return

:SET_4_PHONE_MESSAGES
if
    0ADC: test_cheat "PHONEA4Y"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    037D: set_phone PHONE_A 4messages 'ADMIRAL' 'AMBULAN' 'ANGEL' 'BAGGAGE'
end
if
    0ADC: test_cheat "PHONEB4Y"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    037D: set_phone PHONE_B 4messages 'BENSON' 'BFINJC' 'BLISTAC' 'BLOODRA'
end
return

:SET_5_REPEATED_PHONE_MESSAGES
if
    0ADC: test_cheat "PHONEA5X"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0386: set_phone PHONE_A 5messages_repeated 'ADMIRAL' 'AMBULAN' 'ANGEL' 'BAGGAGE' 'BANSHEE'
end
if
    0ADC: test_cheat "PHONEB5X"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0386: set_phone PHONE_B 5messages_repeated 'BENSON' 'BFINJC' 'BLISTAC' 'BLOODRA' 'BOBCAT'
end
return

:SET_5_PHONE_MESSAGES
if
    0ADC: test_cheat "PHONEA5Y"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0387: set_phone PHONE_A 5messages 'ADMIRAL' 'AMBULAN' 'ANGEL' 'BAGGAGE' 'BANSHEE'
end
if
    0ADC: test_cheat "PHONEB5Y"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0387: set_phone PHONE_B 5messages 'BENSON' 'BFINJC' 'BLISTAC' 'BLOODRA' 'BOBCAT'
end
return

:SET_6_REPEATED_PHONE_MESSAGES
if
    0ADC: test_cheat "PHONEA6X"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0388: set_phone PHONE_A 6messages_repeated 'ADMIRAL' 'AMBULAN' 'ANGEL' 'BAGGAGE' 'BANSHEE' 'BARRCKS'
end
if
    0ADC: test_cheat "PHONEB6X"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0388: set_phone PHONE_B 6messages_repeated 'BENSON' 'BFINJC' 'BLISTAC' 'BLOODRA' 'BOBCAT' 'BOXVILL'
end
return

:SET_6_PHONE_MESSAGES
if
    0ADC: test_cheat "PHONEA6Y"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0389: set_phone PHONE_A 6messages 'ADMIRAL' 'AMBULAN' 'ANGEL' 'BAGGAGE' 'BANSHEE' 'BARRCKS'
end
if
    0ADC: test_cheat "PHONEB6Y"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0389: set_phone PHONE_B 6messages 'BENSON' 'BFINJC' 'BLISTAC' 'BLOODRA' 'BOBCAT' 'BOXVILL'
end
return

:IS_COLLISION_IN_MEMORY
gosub @TEXT_STYLE
if
    03C6:   current_island == 1
then
    0608: show_text_position 10.0 LINE text "03C6: Level 1 is in memory"
else
    0608: show_text_position 10.0 LINE text "03C6: Level 1 is not in memory"
end
return

:IS_CAR_VISIBLY_DAMAGED
gosub @TEXT_STYLE
if
    00E0:   player $PLAYER_CHAR in_any_car
then
    03C1: PLAYER_CAR = player $PLAYER_CHAR car_no_save
    if
        03C9:   car PLAYER_CAR visibly_damaged
    then
        0608: show_text_position 10.0 LINE text "03C9: Car is visibly damaged"
        return
    end
end
0608: show_text_position 10.0 LINE text "03C9: Car is not visibly damaged"
return

:SET_GET_OUT_OF_JAIL_FREE
if
    0ADC: test_cheat "JAILFREE"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0413: set_player $PLAYER_CHAR get_out_of_jail_free 1
end
return

:IS_PLAYER_LIFTING_A_PHONE
gosub @TEXT_STYLE
if
    0447:   player $PLAYER_CHAR lifting_a_phone
then
    0608: show_text_position 10.0 LINE text "0447: Player is lifting phone"
else
    0608: show_text_position 10.0 LINE text "0447: Player is not lifting phone"
end
return

:GET_NUMBER_OF_SEATS_IN_MODEL
if
    00E0:   player $PLAYER_CHAR in_any_car
then
    03C1: PLAYER_CAR = player $PLAYER_CHAR car_no_save
    0441: PLAYER_CAR = car PLAYER_CAR model
    047D: get_number_of_seats_in_model PLAYER_CAR store_to SEATS
else
    SEATS = 0
end
return

:IS_CHAR_IN_ANY_BOAT
gosub @TEXT_STYLE
if
    04A7:   actor $PLAYER_ACTOR in_any_boat
then
    0608: show_text_position 10.0 LINE text "04A7: Player character is in any boat"
else
    0608: show_text_position 10.0 LINE text "04A7: Player character is not in any boat"
end
return

:IS_CHAR_IN_ANY_HELI
gosub @TEXT_STYLE
if
    04A9:   actor $PLAYER_ACTOR in_any_heli
then
    0608: show_text_position 10.0 LINE text "04A9: Player character is in any heli"
else
    0608: show_text_position 10.0 LINE text "04A9: Player character is not in any heli"
end
return

:IS_CHAR_IN_ANY_PLANE
gosub @TEXT_STYLE
if
    04AB:   actor $PLAYER_ACTOR in_any_plane
then
    0608: show_text_position 10.0 LINE text "04AB: Player character is in any plane"
else
    0608: show_text_position 10.0 LINE text "04AB: Player character is not in any plane"
end
return

:IS_CHAR_IN_FLYING_VEHICLE
gosub @TEXT_STYLE
if
    04C8:   actor $PLAYER_ACTOR in_flying_vehicle
then
    0608: show_text_position 10.0 LINE text "04C8: Player character is in flying vehicle"
else
    0608: show_text_position 10.0 LINE text "04C8: Player character is not in flying vehicle"
end
return

:GET_MAX_WANTED_LEVEL
050F: get_max_wanted_level_to MAXWANTED
return

:GET_PLAYER_DRUNKENNESS
052D: get_player $PLAYER_CHAR drunkenness_to DRUNK
if
    0ADC: test_cheat "DRUNK"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    if
        DRUNK == 0
    then
        052C: set_player $PLAYER_CHAR drunk_visuals 255
    else
        052C: set_player $PLAYER_CHAR drunk_visuals 0
    end
end
return

:ADD_LOAN_SHARK_VISITS
if
    0ADC: test_cheat "LOAN"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0530: add_loan_shark_visits 10
end
return

:ADD_MOVIE_STUNTS
if
    0ADC: test_cheat "MOVIE"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0532: add_movie_stunts 10
end
return

:ADD_GARBAGE_PICKUPS
if
    0ADC: test_cheat "GARBAGE"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0535: add_garbage_pickups 10
end
return

:SET_TOP_SHOOTING_RANGE_SCORE
if
    0ADC: test_cheat "RANGESCORE"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0537: set_top_shooting_range_score 10
end
return

:ADD_SHOOTING_RANGE_RANK
if
    0ADC: test_cheat "RANGERANK"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0538: add_shooting_range_rank 10
end
return

:IS_CHAR_TOUCHING_VEHICLE
gosub @TEXT_STYLE
if
    0547:   actor $PLAYER_ACTOR touching_car SPAWN_CAR
then
    0608: show_text_position 10.0 LINE text "0547: Player character is touching spawned vehicle"
else
    0608: show_text_position 10.0 LINE text "0547: Player character is not touching spawned vehicle"
end
return
