{$CLEO .cs}

0000:
const
TEMP_VAR   = 0@
POSX       = 0@
PLAYER_CAR = 0@
RAMP_STATE = 0@
POSY       = 1@
TEMP_VAR2  = 1@
GAME_VERS  = 1@
POSZ       = 2@
TEMP_VAR3  = 2@
SPAWN_CHAR = 3@
SPAWN_CAR  = 4@
LINE       = 5@
TEMP_VAR4  = 6@
SPAWN_OBJ  = 7@
SPAWN_PICK = 8@
PAGE       = 9@
FREEBOMB   = 10@ // bit 0
SETTAXI    = 10@ // bit 1
SWAT_REQ   = 10@ // bit 2
FBI_REQ    = 10@ // bit 3
ARMY_REQ   = 10@ // bit 4
FORCERAIN  = 10@ // bit 5
GANGTHREAT = 10@ // bit 6
GANGPREF   = 10@ // bit 7
SEATS      = 10@ // bits 8 to 11
TEXTBG     = 10@ // bit 12
PASSENGER  = 10@ // bit 13
TEXTFONT   = 10@ // bit 14
BLIP       = 11@
PHONE_A    = 12@
PHONE_B    = 13@
GARAGE     = 14@
CARFIRE    = 15@
PAGE_MAX   = 1
end

while true
    wait 0
    if and
        00E1:   player 0 pressed_button 4 // action
        80E0:   not player $PLAYER_CHAR in_any_car
        $ONMISSION == 0
    then
        $ONMISSION = 1
        break
    end
end

while true
    wait 0
    if
        80E1:   not player 0 pressed_button 4
    then
        break
    end
end

0109: player $PLAYER_CHAR money += 10000
0054: store_player $PLAYER_CHAR position_to POSX POSY POSZ
0247: request_model #COLT45
0247: request_model #MALE01
0247: request_model #DINGHY
038B: load_requested_models
POSX += 4.0
009A: SPAWN_CHAR = create_actor_pedtype 4 model #MALE01 at POSX POSY POSZ
POSX += 4.0
00A5: SPAWN_CAR = create_car #DINGHY at POSX POSY POSZ
0229: set_car SPAWN_CAR color_to 6 75
POSX += 4.0
0213: SPAWN_PICK = create_pickup #COLT45 type 2 at POSX POSY POSZ
POSX += 4.0
029B: SPAWN_OBJ = init_object #ELECTRICGATE at POSX POSY POSZ
0249: release_model #COLT45
0249: release_model #DINGHY
01B1: give_player $PLAYER_CHAR weapon 17 ammo 10
024A: PHONE_A = get_phone_at -1482.8 -825.259
024C: set_phone PHONE_A message 'ADMIRAL'
024A: PHONE_B = get_phone_at -1483.32 -824.733
0219: GARAGE = create_garage_type 32 door -836.79 1307.78 10.04 to -838.88 1313.67 15.04 depth -833.59 1308.72
10@ = 0
021D: set_free_bomb_shop_to 0
021F: set_all_taxi_lights 0
02BC: set_swat_required 0
02BD: set_fbi_required 0
02BE: set_army_required 0
0421: force_rain 0
03F2: pedgroup 8 remove_threat 1
PAGE = 0
03F0: enable_text_draw 1
059D: shuffle_card_decks 2

while true
    wait 10
    LINE = 0
    gosub @IS_CHAR_STILL_ALIVE
    gosub @IS_CAR_STILL_ALIVE
    gosub @RETURN_TRUE
    gosub @RETURN_FALSE
    gosub @ADD_AMMO_TO_PLAYER
    gosub @IS_PLAYER_STILL_ALIVE
    gosub @HAS_PLAYER_BEEN_ARRESTED
    gosub @CHANGE_CAR_LOCK
    gosub @SHAKE_CAM_WITH_POINT
    gosub @IS_CAR_DEAD_IN_AREA_2D
    gosub @IS_CAR_DEAD_IN_AREA_3D
    gosub @IS_CAR_IN_AIR
    gosub @ADD_BLIP_FOR_OBJECT_OLD
    gosub @IS_PLAYER_TOUCHING_OBJECT
    gosub @IS_CHAR_TOUCHING_OBJECT
    gosub @SET_CHAR_AMMO
    gosub @ACTIVATE_CRANE
    gosub @DEACTIVATE_CRANE
    gosub @SET_FREE_BOMBS
    gosub @SET_ALL_TAXI_LIGHTS
    gosub @IS_CAR_ARMED_WITH_ANY_BOMB
    gosub @IS_CAR_ARMED_WITH_BOMB
    gosub @IS_PLAYER_TOUCHING_OBJECT_ON_FOOT
    gosub @IS_CHAR_TOUCHING_OBJECT_ON_FOOT
    gosub @ARM_CAR_WITH_BOMB
    gosub @SET_REPEATED_PHONE_MESSAGE
    gosub @SET_PHONE_MESSAGE
    gosub @HAS_PHONE_DISPLAYED_MESSAGE
    gosub @RESTART_CRITICAL_MISSION
    gosub @IS_TAXI
    gosub @ACTIVATE_GARAGE
    gosub @IS_BOAT
    gosub @IS_CHAR_STOPPED
    gosub @MESSAGE_WAIT
    gosub @ADD_PARTICLE_EFFECT
    gosub @ADD_SPRITE_BLIP_FOR_CAR
    gosub @ADD_SPRITE_BLIP_FOR_CHAR
    gosub @ADD_SPRITE_BLIP_FOR_OBJECT
    gosub @DEACTIVATE_GARAGE
    gosub @SET_SWAT_REQUIRED
    gosub @SET_FBI_REQUIRED
    gosub @SET_ARMY_REQUIRED
    gosub @START_PACMAN_RACE
    gosub @CLEAR_PACMAN
    gosub @START_PACMAN_SCRAMBLE
    gosub @CLEAR_NUMBER_OF_POWER_PILLS_CARRIED
    gosub @IS_CHAR_SHOOTING_IN_AREA
    gosub @CLEAR_NUMBER_OF_POWER_PILLS_EATEN
    gosub @ADD_POWER_PILL
    gosub @IS_PROJECTILE_IN_AREA
    gosub @DROP_MINE
    gosub @DROP_NAUTICAL_MINE
    gosub @ACTIVATE_CRUSHER_CRANE
    gosub @PRINT_WITH_NUMBERS
    gosub @IS_FIRST_CAR_COLOUR
    gosub @IS_SECOND_CAR_COLOUR
    gosub @SET_CAR_VISIBLE
    gosub @SET_TEXT_BACKGROUND_COLOUR
    gosub @SET_TEXT_FONT
    gosub @IS_NASTY_GAME
    gosub @IS_EXPLOSION_IN_AREA
    gosub @IS_EXPLOSION_IN_ZONE
    gosub @START_KILL_FRENZY_HEADSHOT
    gosub @ACTIVATE_MILITARY_CRANE
    gosub @PRINT_WITH_NUMBERS_BIG
    gosub @SET_2_REPEATED_PHONE_MESSAGES
    gosub @SET_2_PHONE_MESSAGES
    gosub @SET_3_REPEATED_PHONE_MESSAGES
    gosub @SET_3_PHONE_MESSAGES
    gosub @SET_4_REPEATED_PHONE_MESSAGES
    gosub @SET_4_PHONE_MESSAGES
    gosub @GIVE_PLAYER_DETONATOR
    gosub @SET_5_REPEATED_PHONE_MESSAGES
    gosub @SET_5_PHONE_MESSAGES
    gosub @SET_6_REPEATED_PHONE_MESSAGES
    gosub @SET_6_PHONE_MESSAGES
    gosub @IS_CRANE_LIFTING_CAR
    gosub @IS_PHONE_DISPLAYING_MESSAGE
    gosub @IS_COLLISION_IN_MEMORY
    gosub @IS_CAR_VISIBLY_DAMAGED
    gosub @ADD_BLIP_FOR_PICKUP_OLD
    gosub @ADD_SPRITE_BLIP_FOR_PICKUP
    gosub @IS_CHAR_IN_CONTROL
    gosub @HAS_MILITARY_CRANE_COLLECTED_ALL_CARS
    gosub @SET_GANG_PED_MODEL_PREFERENCE
    gosub @SET_GET_OUT_OF_JAIL_FREE
    gosub @IS_CAR_DOOR_CLOSED
    gosub @REGISTER_KILL_FRENZY_PASSED
    gosub @SET_CHAR_SAY
    gosub @OVERRIDE_HOSPITAL_LEVEL
    gosub @OVERRIDE_POLICE_STATION_LEVEL
    gosub @FORCE_RAIN
    gosub @DOES_GARAGE_CONTAIN_CAR
    gosub @IS_THREAT_FOR_PED_TYPE
    gosub @GET_CHAR_IN_CAR_PASSENGER_SEAT
    gosub @IS_PLAYER_LIFTING_A_PHONE
    gosub @SET_SCRIPT_FIRE_AUDIO
    gosub @GET_NUMBER_OF_SEATS_IN_MODEL
    gosub @IS_CHAR_IN_ANY_BOAT
    gosub @IS_CHAR_IN_ANY_HELI
    gosub @IS_CHAR_IN_ANY_PLANE
    gosub @IS_CHAR_IN_FLYING_VEHICLE
    gosub @GET_MAX_WANTED_LEVEL
    gosub @PRINT_HELP
    gosub @GET_PLAYER_DRUNKENNESS
    gosub @ADD_LOAN_SHARK_VISITS
    gosub @ADD_MOVIE_STUNTS
    gosub @ADD_GARBAGE_PICKUPS
    gosub @SET_TOP_SHOOTING_RANGE_SCORE
    gosub @ADD_SHOOTING_RANGE_RANK
    gosub @IS_CHAR_TOUCHING_VEHICLE

    if and
        00E1:   player 0 pressed_button 4 // action
        80E0:   not player $PLAYER_CHAR in_any_car
    then
        01C2: mark_actor_as_no_longer_needed SPAWN_CHAR
        01C3: mark_car_as_no_longer_needed SPAWN_CAR
        0215: destroy_pickup SPAWN_PICK
        01C4: mark_object_as_no_longer_needed SPAWN_OBJ
        0164: disable_marker BLIP
        02C6: clear_pacman
        $ONMISSION = 0
        03F0: enable_text_draw 0
        05DC: end_custom_thread
    end
    // next page
    if
        05EE:   key_pressed 0xBE // .
    then
        while true
            wait 0
            if
                85EE:   not key_pressed 0xBE
            then
                break
            end
        end
        PAGE += 1
        if
            PAGE > PAGE_MAX
        then
            PAGE = 0
        end
    end
    // previous page
    if
        05EE:   key_pressed 0xBC // ,
    then
        while true
            wait 0
            if
                85EE:   not key_pressed 0xBC
            then
                break
            end
        end
        PAGE -= 1
        if
            0 > PAGE
        then
            PAGE = 1
        end
    end
    if
        8447:   not player $PLAYER_CHAR lifting_a_phone
    then
        if
            PAGE == 0
        then
            05F9: TEMP_VAR = SEATS AND 0x00000F00
            05FE: TEMP_VAR = TEMP_VAR SHR 8
            050F: get_max_wanted_level_to TEMP_VAR2
            052D: get_player $PLAYER_CHAR drunkenness_to TEMP_VAR3
            00E2: get_player 0 key 6 state_to TEMP_VAR4
            // aim_key, maxwanted, drunk, seats, var, page
            0308: text_6numbers 'HJSTATW' TEMP_VAR4 TEMP_VAR2 TEMP_VAR3 TEMP_VAR 10@ PAGE time 10 flag 5
        else
            02C5: get_number_of_power_pills_eaten TEMP_VAR
            02C8: get_number_of_power_pills_carried TEMP_VAR2
            059E: fetch_next_card TEMP_VAR4
            TEMP_VAR3 = 0
            if and
                8119:   not car SPAWN_CAR wrecked
                00DB:   actor SPAWN_CHAR in_car SPAWN_CAR
            then
                0432: get_actor_in_car SPAWN_CAR passenger_seat 0 store_to TEMP_VAR3
            end
            // pm_eaten, pm_carried, passenger, card, var, page
            0308: text_6numbers 'HJSTATW' TEMP_VAR TEMP_VAR2 TEMP_VAR3 TEMP_VAR4 10@ PAGE time 10 flag 5
        end
    end
    // manipulate spawned car
    if
        0ADC: test_cheat "PUTCAR"
    then
        018C: play_sound 1 at 0.0 0.0 0.0
        0054: store_player $PLAYER_CHAR position_to POSX POSY POSZ
        00AB: put_car SPAWN_CAR at POSX POSY POSZ
    end
    if
        0ADC: test_cheat "CADDY"
    then
        018C: play_sound 1 at 0.0 0.0 0.0
        01C3: mark_car_as_no_longer_needed SPAWN_CAR
        00A6: destroy_car SPAWN_CAR
        0054: store_player $PLAYER_CHAR position_to POSX POSY POSZ
        0247: request_model #CADDY
        038B: load_requested_models
        00A5: SPAWN_CAR = create_car #CADDY at POSX POSY POSZ
        0249: release_model #CADDY
    end
    if
        0ADC: test_cheat "DINGHY"
    then
        018C: play_sound 1 at 0.0 0.0 0.0
        01C3: mark_car_as_no_longer_needed SPAWN_CAR
        00A6: destroy_car SPAWN_CAR
        0054: store_player $PLAYER_CHAR position_to POSX POSY POSZ
        0247: request_model #DINGHY
        038B: load_requested_models
        00A5: SPAWN_CAR = create_car #DINGHY at POSX POSY POSZ
        0249: release_model #DINGHY
    end
    if
        0ADC: test_cheat "HUNTER"
    then
        018C: play_sound 1 at 0.0 0.0 0.0
        01C3: mark_car_as_no_longer_needed SPAWN_CAR
        00A6: destroy_car SPAWN_CAR
        0054: store_player $PLAYER_CHAR position_to POSX POSY POSZ
        0247: request_model #HUNTER
        038B: load_requested_models
        00A5: SPAWN_CAR = create_car #HUNTER at POSX POSY POSZ
        0249: release_model #HUNTER
    end
end

:TEXT_STYLE
05F9: TEMP_VAR = TEXTBG AND 0x1000
if
    not TEMP_VAR == 0
then
    0345: set_text_draw_in_box 1
    0346: set_text_draw_background_color 255 0 0 128
end
05F9: TEMP_VAR = TEXTFONT AND 0x4000
if
    not TEMP_VAR == 0
then
    0349: set_text_draw_font 2
end
0343: set_text_linewidth 600.0
033F: set_text_draw_letter_width_height 0.3 1.2
LINE += 11.0
return

:IS_CHAR_STILL_ALIVE
if
    PAGE == 0
then
    gosub @TEXT_STYLE
    if
        00A2:   actor SPAWN_CHAR alive
    then
        0608: show_text_position 10.0 LINE text "00A2: Spawned character is still alive"
    else
        0608: show_text_position 10.0 LINE text "00A2: Spawned character is dead"
    end
end
return

:IS_CAR_STILL_ALIVE
if
    PAGE == 0
then
    gosub @TEXT_STYLE
    if
        00AC:   car SPAWN_CAR alive
    then
        0608: show_text_position 10.0 LINE text "00AC: Spawned boat is still alive"
    else
        0608: show_text_position 10.0 LINE text "00AC: Spawned boat is dead"
    end
end
return

:RETURN_TRUE
if
    PAGE == 0
then
    gosub @TEXT_STYLE
    if
        00C5:   true
    then
        0608: show_text_position 10.0 LINE text "00C5: Returned true"
    else
        0608: show_text_position 10.0 LINE text "00C5: Returned false"
    end
end
return

:RETURN_FALSE
if
    PAGE == 0
then
    gosub @TEXT_STYLE
    if
        00C6:   false
    then
        0608: show_text_position 10.0 LINE text "00C6: Returned true"
    else
        0608: show_text_position 10.0 LINE text "00C6: Returned false"
    end
end
return

:ADD_AMMO_TO_PLAYER
if
    0ADC: test_cheat "ADDAMMO"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0113: add_ammo_to_player $PLAYER_CHAR weapon 17 ammo 10
end
return

:IS_PLAYER_STILL_ALIVE
if
    PAGE == 0
then
    gosub @TEXT_STYLE
    if
        0116:   player $PLAYER_CHAR alive
    then
        0608: show_text_position 10.0 LINE text "0116: Player is alive"
    else
        0608: show_text_position 10.0 LINE text "0116: Player is dead"
    end
end
return

:HAS_PLAYER_BEEN_ARRESTED
if
    PAGE == 0
then
    gosub @TEXT_STYLE
    if
        0130:   player $PLAYER_CHAR busted
    then
        0608: show_text_position 10.0 LINE text "0130: Player has been arrested"
    else
        0608: show_text_position 10.0 LINE text "0130: Player has not been arrested"
    end
end
return

:CHANGE_CAR_LOCK
if
    00E0:   player $PLAYER_CHAR in_any_car
then
    if
        0ADC: test_cheat "LOCK"
    then
        018C: play_sound 1 at 0.0 0.0 0.0
        03C1: PLAYER_CAR = player $PLAYER_CHAR car_no_save
        0135: change_car PLAYER_CAR lock 2
    end
end
return

:SHAKE_CAM_WITH_POINT
if
    0ADC: test_cheat "SHAKE"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0054: store_player $PLAYER_CHAR position_to POSX POSY POSZ
    POSX += 100.0
    0136: shake_camera 5000 with_point POSX POSY POSZ
end
return

:IS_CAR_DEAD_IN_AREA_2D
if
    PAGE == 0
then
    gosub @TEXT_STYLE
    if
        013B:   car SPAWN_CAR dead_in_rectangle 238.4 -1282.2 242.4 -1278.2 1
    then
        0608: show_text_position 10.0 LINE text "013B: Spawned vehicle is dead in marker"
    else
        0608: show_text_position 10.0 LINE text "013B: Spawned vehicle is not dead in marker"
    end
end
return

:IS_CAR_DEAD_IN_AREA_3D
if
    PAGE == 0
then
    gosub @TEXT_STYLE
    if
        013C:   car SPAWN_CAR dead_in_cube 242.4 -1282.2 8.5 246.4 -1278.2 11.5 1
    then
        0608: show_text_position 10.0 LINE text "013C: Spawned vehicle is dead in marker"
    else
        0608: show_text_position 10.0 LINE text "013C: Spawned vehicle is not dead in marker"
    end
end
return

:IS_CAR_IN_AIR
if
    PAGE == 0
then
    gosub @TEXT_STYLE
    if
        00E0:   player $PLAYER_CHAR in_any_car
    then
        03C1: PLAYER_CAR = player $PLAYER_CHAR car_no_save
        if
            015E:   car PLAYER_CAR in_air
        then
            0608: show_text_position 10.0 LINE text "015E: Car is in air"
            return
        end
    end
    0608: show_text_position 10.0 LINE text "015E: Car is not in air"
end
return

:ADD_BLIP_FOR_OBJECT_OLD
if
    0ADC: test_cheat "OBJBLIP"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0164: disable_marker BLIP
    0163: create_marker_above_object SPAWN_OBJ color 0 visibility 3 handle BLIP
end
return

:IS_PLAYER_TOUCHING_OBJECT
if
    PAGE == 0
then
    gosub @TEXT_STYLE
    if
        0178:   player $PLAYER_CHAR touching_object SPAWN_OBJ
    then
        0608: show_text_position 10.0 LINE text "0178: Player is touching spawned object"
    else
        0608: show_text_position 10.0 LINE text "0178: Player is not touching spawned object"
    end
end
return

:IS_CHAR_TOUCHING_OBJECT
if
    PAGE == 0
then
    gosub @TEXT_STYLE
    if
        0179:   actor $PLAYER_ACTOR touching_object SPAWN_OBJ
    then
        0608: show_text_position 10.0 LINE text "0179: Player character is touching spawned object"
    else
        0608: show_text_position 10.0 LINE text "0179: Player character is not touching spawned object"
    end
end
return

:SET_CHAR_AMMO
if
    0ADC: test_cheat "SETAMMO"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    017B: set_actor $PLAYER_ACTOR weapon 17 ammo_to 100
end
return

:ACTIVATE_CRANE
if
    0ADC: test_cheat "CRANEA"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    01EF: deactivate_crane -964.0 -1490.0
    01EE: activate_crane -964.0 -1490.0 -974.0 -1500.0 -954.0 -1480.0 -951.0 -1525.0 20.0 160.0
end
return

:DEACTIVATE_CRANE
if
    0ADC: test_cheat "CRANED"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    01EF: deactivate_crane -964.0 -1490.0
end
return

:SET_FREE_BOMBS
if
    0ADC: test_cheat "FREEBOMBS"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    05F9: TEMP_VAR = FREEBOMB AND 1
    if
        TEMP_VAR == 0
    then
        05FA: FREEBOMB = FREEBOMB OR 1
        021D: set_free_bomb_shop_to 1
    else
        05F9: FREEBOMB = FREEBOMB AND 0xFFFFFFFE
        021D: set_free_bomb_shop_to 0
    end
end
return

:SET_ALL_TAXI_LIGHTS
if
    0ADC: test_cheat "TAXI"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    05F9: TEMP_VAR = SETTAXI AND 2
    if
        TEMP_VAR == 0
    then
        05FA: SETTAXI = SETTAXI OR 2
        021F: set_all_taxi_lights 1
    else
        05F9: SETTAXI = SETTAXI AND 0xFFFFFFFD
        021F: set_all_taxi_lights 0
    end
end
return

:IS_CAR_ARMED_WITH_ANY_BOMB
if
    PAGE == 0
then
    gosub @TEXT_STYLE
    if
        00E0:   player $PLAYER_CHAR in_any_car
    then
        03C1: PLAYER_CAR = player $PLAYER_CHAR car_no_save
        if
            0220:   car PLAYER_CAR has_car_bomb
        then
            0608: show_text_position 10.0 LINE text "0220: Car is armed with any bomb"
            return
        end
    end
    0608: show_text_position 10.0 LINE text "0220: Car is not armed with any bomb"
end
return

:IS_CAR_ARMED_WITH_BOMB
if
    PAGE == 0
then
    gosub @TEXT_STYLE
    if
        00E0:   player $PLAYER_CHAR in_any_car
    then
        03C1: PLAYER_CAR = player $PLAYER_CHAR car_no_save
        if
            0228:   car PLAYER_CAR bomb_status == 4
        then
            0608: show_text_position 10.0 LINE text "0228: Car is armed with bomb 4"
            return
        end
    end
    0608: show_text_position 10.0 LINE text "0228: Car is not armed with bomb 4"
end
return

:IS_PLAYER_TOUCHING_OBJECT_ON_FOOT
if
    PAGE == 0
then
    gosub @TEXT_STYLE
    if
        023A:   player $PLAYER_CHAR touching_object SPAWN_OBJ on_foot
    then
        0608: show_text_position 10.0 LINE text "023A: Player is touching spawned object on foot"
    else
        0608: show_text_position 10.0 LINE text "023A: Player is not touching spawned object on foot"
    end
end
return

:IS_CHAR_TOUCHING_OBJECT_ON_FOOT
if
    PAGE == 0
then
    gosub @TEXT_STYLE
    if
        023B:   actor $PLAYER_ACTOR touching_object SPAWN_OBJ on_foot
    then
        0608: show_text_position 10.0 LINE text "023B: Player character is touching spawned object on foot"
    else
        0608: show_text_position 10.0 LINE text "023B: Player character is not touching spawned object on foot"
    end
end
return

:ARM_CAR_WITH_BOMB
if
    00E0:   player $PLAYER_CHAR in_any_car
then
    if
        0ADC: test_cheat "ARMBOMB"
    then
        018C: play_sound 1 at 0.0 0.0 0.0
        03C1: PLAYER_CAR = player $PLAYER_CHAR car_no_save
        0242: set_car PLAYER_CAR bomb_status_to 1
    end
end
return

:SET_REPEATED_PHONE_MESSAGE
if
    0ADC: test_cheat "PHONEA1X"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    024B: set_phone PHONE_A message_repeated 'ADMIRAL'
end
if
    0ADC: test_cheat "PHONEB1X"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    024B: set_phone PHONE_B message_repeated 'BENSON'
end
return

:SET_PHONE_MESSAGE
if
    0ADC: test_cheat "PHONEA1Y"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    024C: set_phone PHONE_A message 'ADMIRAL'
end
if
    0ADC: test_cheat "PHONEB1Y"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    024C: set_phone PHONE_B message 'BENSON'
end
return

:HAS_PHONE_DISPLAYED_MESSAGE
if
    PAGE == 0
then
    gosub @TEXT_STYLE
    if
        024D:   phone PHONE_A has_displayed_message
    then
        0608: show_text_position 10.0 LINE text "024D: Phone-A has displayed message"
    else
        0608: show_text_position 10.0 LINE text "024D: Phone-A has not displayed message"
    end
end
return

:RESTART_CRITICAL_MISSION
if
    0ADC: test_cheat "RESTART"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0255: set_critical_mission_restart_at -833.811 1304.07 10.5131 angle 200.4458
end
return

:IS_TAXI
if
    PAGE == 0
then
    gosub @TEXT_STYLE
    if
        00E0:   player $PLAYER_CHAR in_any_car
    then
        03C1: PLAYER_CAR = player $PLAYER_CHAR car_no_save
        if
            0295:   is PLAYER_CAR taxi
        then
            0608: show_text_position 10.0 LINE text "0295: Is a taxi"
            return
        end
    end
    0608: show_text_position 10.0 LINE text "0295: Is not a taxi"
end
return

:ACTIVATE_GARAGE
if
    0ADC: test_cheat "GARAGEA"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0299: activate_garage GARAGE
end
return

:IS_BOAT
if
    PAGE == 0
then
    gosub @TEXT_STYLE
    if
        00E0:   player $PLAYER_CHAR in_any_car
    then
        03C1: PLAYER_CAR = player $PLAYER_CHAR car_no_save
        if
            029C:   is PLAYER_CAR boat
        then
            0608: show_text_position 10.0 LINE text "029C: Is a boat"
            return
        end
    end
    0608: show_text_position 10.0 LINE text "029C: Is not a boat"
end
return

:IS_CHAR_STOPPED
if
    PAGE == 0
then
    gosub @TEXT_STYLE
    if
        02A0:   actor $PLAYER_ACTOR stopped
    then
        0608: show_text_position 10.0 LINE text "02A0: Player character is 'stopped'"
    else
        0608: show_text_position 10.0 LINE text "02A0: Player character is not 'stopped'"
    end
end
return

:MESSAGE_WAIT
if
    0ADC: test_cheat "MESSAGEWAIT"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    00BC: text_highpriority 'ADMIRAL' 2500 ms 1
    02A1: message_wait 2500 1
    00BC: text_highpriority 'BENSON' 2500 ms 1
    02A1: message_wait 2500 1
end
return

:ADD_PARTICLE_EFFECT
if
    0ADC: test_cheat "PARTICLE"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0054: store_player $PLAYER_CHAR position_to POSX POSY POSZ
    02A2: create_particle 12 at POSX POSY POSZ 1
end
return

:ADD_SPRITE_BLIP_FOR_CAR
if
    0ADC: test_cheat "CARSPRITE"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0164: disable_marker BLIP
    02A4: create_marker_above_car SPAWN_CAR icon 36 handle BLIP
end
return

:ADD_SPRITE_BLIP_FOR_CHAR
if
    0ADC: test_cheat "PEDSPRITE"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0164: disable_marker BLIP
    02A5: create_marker_above_actor SPAWN_CHAR icon 34 handle BLIP
end
return

:ADD_SPRITE_BLIP_FOR_OBJECT
if
    0ADC: test_cheat "OBJSPRITE"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0164: disable_marker BLIP
    02A6: create_marker_above_object SPAWN_OBJ icon 38 handle BLIP
end
return

:DEACTIVATE_GARAGE
if
    0ADC: test_cheat "GARAGED"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    02B9: deactivate_garage GARAGE
end
return

:SET_SWAT_REQUIRED
if
    0ADC: test_cheat "SWATREQ"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    05F9: TEMP_VAR = SWAT_REQ AND 4
    if
        TEMP_VAR == 0
    then
        05FA: SWAT_REQ = SWAT_REQ OR 4
        02BC: set_swat_required 1
    else
        05F9: SWAT_REQ = SWAT_REQ AND 0xFFFFFFFB
        02BC: set_swat_required 0
    end
end
return

:SET_FBI_REQUIRED
if
    0ADC: test_cheat "FBIREQ"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    05F9: TEMP_VAR = FBI_REQ AND 8
    if
        TEMP_VAR == 0
    then
        05FA: FBI_REQ = FBI_REQ OR 8
        02BD: set_fbi_required 1
    else
        05F9: FBI_REQ = FBI_REQ AND 0xFFFFFFF7
        02BD: set_fbi_required 0
    end
end
return

:SET_ARMY_REQUIRED
if
    0ADC: test_cheat "ARMYREQ"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    05F9: TEMP_VAR = ARMY_REQ AND 0x10
    if
        TEMP_VAR == 0
    then
        05FA: ARMY_REQ = ARMY_REQ OR 0x10
        02BE: set_army_required 1
    else
        05F9: ARMY_REQ = ARMY_REQ AND 0xFFFFFFEF
        02BE: set_army_required 0
    end
end
return

:START_PACMAN_RACE
if
    0ADC: test_cheat "PMRACE"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    02C6: clear_pacman
    02C3: start_pacman_race 0
end
return

:CLEAR_PACMAN
if
    0ADC: test_cheat "CLEARPM"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    02C6: clear_pacman
end
return

:START_PACMAN_SCRAMBLE
if
    0ADC: test_cheat "PMSCRAM"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    02C6: clear_pacman
    0054: store_player $PLAYER_CHAR position_to POSX POSY POSZ
    02C7: start_pacman_scramble POSX POSY POSZ radius 10.0 count 10
end
return

:CLEAR_NUMBER_OF_POWER_PILLS_CARRIED
if
    0ADC: test_cheat "CLEARSCRAM"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    02C9: clear_number_of_power_pills_carried
end
return

:IS_CHAR_SHOOTING_IN_AREA
if
    PAGE == 0
then
    gosub @TEXT_STYLE
    if
        02D6:   actor $PLAYER_ACTOR shooting_in_area -838.88 1307.78 -833.59 1313.67 0
    then
        0608: show_text_position 10.0 LINE text "02D6: Player character is shooting in garage area"
    else
        0608: show_text_position 10.0 LINE text "02D6: Player character is not shooting in garage area"
    end
end
return

:CLEAR_NUMBER_OF_POWER_PILLS_EATEN
if
    0ADC: test_cheat "CLEARRACE"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    02D9: clear_number_of_power_pills_eaten
end
return

:ADD_POWER_PILL
if
    0ADC: test_cheat "PMONE"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    02C6: clear_pacman
    0054: store_player $PLAYER_CHAR position_to POSX POSY POSZ
    02DA: add_power_pill POSX POSY POSZ
end
return

:IS_PROJECTILE_IN_AREA
if
    PAGE == 0
then
    gosub @TEXT_STYLE
    if
        02EE:   projectile_in_cube -838.88 1307.78 10.04 -833.59 1313.67 15.04
    then
        0608: show_text_position 10.0 LINE text "02EE: Projectile is in garage area"
    else
        0608: show_text_position 10.0 LINE text "02EE: Projectile is not in garage area"
    end
end
return

:DROP_MINE
if
    0ADC: test_cheat "MINEA"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0054: store_player $PLAYER_CHAR position_to POSX POSY POSZ
    02F0: create_explosive_barrel_at POSX POSY -100.0
end
return

:DROP_NAUTICAL_MINE
if
    0ADC: test_cheat "MINEB"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0054: store_player $PLAYER_CHAR position_to POSX POSY POSZ
    02F1: create_explosive_barrel_at POSX POSY -100.0
end
return

:ACTIVATE_CRUSHER_CRANE
if
    0ADC: test_cheat "CRANEC"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    01EF: deactivate_crane -964.0 -1490.0
    02FB: create_crusher_crane -964.0 -1490.0 -974.0 -1500.0 -954.0 -1480.0 -951.0 -1525.0 20.0 160.0
end
return

:PRINT_WITH_NUMBERS
if
    0ADC: test_cheat "PRINTNUM"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    02FC: text_2numbers 'HJSTATW' 1 2 500 ms 1
    wait 500
    018C: play_sound 1 at 0.0 0.0 0.0
    0300: text_3numbers_highpriority 'HJSTATW' 1 2 3 500 ms 1
    wait 500
    018C: play_sound 1 at 0.0 0.0 0.0
    0303: text_4numbers_highpriority 'HJSTATW' 1 2 3 4 500 ms 1
    wait 500
    018C: play_sound 1 at 0.0 0.0 0.0
    0305: text_5numbers 'HJSTATW' 1 2 3 4 5 500 ms 1
    wait 500
    018C: play_sound 1 at 0.0 0.0 0.0
    0306: text_5numbers_highpriority 'HJSTATW' 1 2 3 4 5 500 ms 1
    wait 500
    018C: play_sound 1 at 0.0 0.0 0.0
    0309: text_6numbers_highpriority 'HJSTATW' 1 2 3 4 5 6 500 ms 1
    wait 500
end
return

:IS_FIRST_CAR_COLOUR
if
    PAGE == 1
then
    gosub @TEXT_STYLE
    if
        00E0:   player $PLAYER_CHAR in_any_car
    then
        03C1: PLAYER_CAR = player $PLAYER_CHAR car_no_save
        if
            031B:   car PLAYER_CAR primary_color 6
        then
            0608: show_text_position 10.0 LINE text "031B: Car primary colour is 6"
            return
        end
    end
    0608: show_text_position 10.0 LINE text "031B: Car primary colour is not 6"
end
return

:IS_SECOND_CAR_COLOUR
if
    PAGE == 1
then
    gosub @TEXT_STYLE
    if
        00E0:   player $PLAYER_CHAR in_any_car
    then
        03C1: PLAYER_CAR = player $PLAYER_CHAR car_no_save
        if
            031C:   car PLAYER_CAR secondary_color 75
        then
            0608: show_text_position 10.0 LINE text "031C: Car secondary colour is 75"
            return
        end
    end
    0608: show_text_position 10.0 LINE text "031C: Car secondary colour is not 75"
end
return

:SET_CAR_VISIBLE
if
    00E0:   player $PLAYER_CHAR in_any_car
then
    if
        0ADC: test_cheat "INVISIBLE"
    then
        018C: play_sound 1 at 0.0 0.0 0.0
        03C1: PLAYER_CAR = player $PLAYER_CHAR car_no_save
        0338: set_car PLAYER_CAR visible 0
    end
end
return

:SET_TEXT_BACKGROUND_COLOUR
if
    0ADC: test_cheat "BACKGROUND"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    05F9: TEMP_VAR = TEXTBG AND 0x1000
    if
        TEMP_VAR == 0
    then
        05FA: TEXTBG = TEXTBG OR 0x1000
    else
        05F9: TEXTBG = TEXTBG AND 0xFFFFEFFF
    end
end
return

:SET_TEXT_FONT
if
    0ADC: test_cheat "FONT"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    05F9: TEMP_VAR = TEXTFONT AND 0x4000
    if
        TEMP_VAR == 0
    then
        05FA: TEXTFONT = TEXTFONT OR 0x4000
    else
        05F9: TEXTFONT = TEXTFONT AND 0xFFFFBFFF
    end
end
return

:IS_NASTY_GAME
if
    PAGE == 1
then
    gosub @TEXT_STYLE
    if
        0351:   gore_enabled
    then
        0608: show_text_position 10.0 LINE text "0351: Gore is enabled"
    else
        0608: show_text_position 10.0 LINE text "0351: Gore is disabled"
    end
end
return

:IS_EXPLOSION_IN_AREA
if
    PAGE == 1
then
    gosub @TEXT_STYLE
    if
        0356:   explosion_type 1 in_cube -838.88 1307.78 10.04 -833.59 1313.67 15.04
    then
        0608: show_text_position 10.0 LINE text "0356: Molotov explosion in garage area"
    else
        0608: show_text_position 10.0 LINE text "0356: No Molotov explosion in garage area"
    end
end
return

:IS_EXPLOSION_IN_ZONE
if
    PAGE == 1
then
    gosub @TEXT_STYLE
    if
        0357:   explosion_type 1 in_zone 'DTOWN'
    then
        0608: show_text_position 10.0 LINE text "0357: Molotov explosion in Downtown area"
    else
        0608: show_text_position 10.0 LINE text "0357: No Molotov explosion in Downtown area"
    end
end
return

:START_KILL_FRENZY_HEADSHOT
01FA: RAMP_STATE = rampage_status
if and
    0ADC: test_cheat "RAMPAGE"
    not RAMP_STATE == 1
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0247: request_model #M4
    038B: load_requested_models
    0367: init_headshot_rampage 'RAMPAGE' weapon 26 time 120000 targets 10 -1 -1 -1 -1 0
    0249: release_model #M4
end
return

:ACTIVATE_MILITARY_CRANE
if
    0ADC: test_cheat "CRANEM"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    01EF: deactivate_crane -964.0 -1490.0
    0368: create_ev_crane -964.0 -1490.0 -974.0 -1500.0 -954.0 -1480.0 -951.0 -1525.0 20.0 160.0
end
return

:PRINT_WITH_NUMBERS_BIG
if
    0ADC: test_cheat "PRINTBIGNUM"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0218: text_1number_styled 'HJSTATW' 1 time 500 style 6
    wait 500
    018C: play_sound 1 at 0.0 0.0 0.0
    036E: text_3numbers_styled 'HJSTATW' numbers 1 2 3 time 500 style 6
    wait 500
    018C: play_sound 1 at 0.0 0.0 0.0
    036F: text_4numbers_styled 'HJSTATW' numbers 1 2 3 4 time 500 style 6
    wait 500
    018C: play_sound 1 at 0.0 0.0 0.0
    0370: text_5numbers_styled 'HJSTATW' numbers 1 2 3 4 5 time 500 style 6
    wait 500
    018C: play_sound 1 at 0.0 0.0 0.0
    0371: text_6numbers_styled 'HJSTATW' numbers 1 2 3 4 5 6 time 500 style 6
    wait 500
end
return

:SET_2_REPEATED_PHONE_MESSAGES
if
    0ADC: test_cheat "PHONEA2X"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0378: set_phone PHONE_A 2messages_repeated 'ADMIRAL' 'AMBULAN'
end
if
    0ADC: test_cheat "PHONEB2X"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0378: set_phone PHONE_B 2messages_repeated 'BENSON' 'BFINJC'
end
return

:SET_2_PHONE_MESSAGES
if
    0ADC: test_cheat "PHONEA2Y"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0379: set_phone PHONE_A 2messages 'ADMIRAL' 'AMBULAN'
end
if
    0ADC: test_cheat "PHONEB2Y"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0379: set_phone PHONE_B 2messages 'BENSON' 'BFINJC'
end
return

:SET_3_REPEATED_PHONE_MESSAGES
if
    0ADC: test_cheat "PHONEA3X"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    037A: set_phone PHONE_A 3messages_repeated 'ADMIRAL' 'AMBULAN' 'ANGEL'
end
if
    0ADC: test_cheat "PHONEB3X"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    037A: set_phone PHONE_B 3messages_repeated 'BENSON' 'BFINJC' 'BLISTAC'
end
return

:SET_3_PHONE_MESSAGES
if
    0ADC: test_cheat "PHONEA3Y"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    037B: set_phone PHONE_A 3messages 'ADMIRAL' 'AMBULAN' 'ANGEL'
end
if
    0ADC: test_cheat "PHONEB3Y"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    037B: set_phone PHONE_B 3messages 'BENSON' 'BFINJC' 'BLISTAC'
end
return

:SET_4_REPEATED_PHONE_MESSAGES
if
    0ADC: test_cheat "PHONEA4X"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    037C: set_phone PHONE_A 4messages_repeated 'ADMIRAL' 'AMBULAN' 'ANGEL' 'BAGGAGE'
end
if
    0ADC: test_cheat "PHONEB4X"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    037C: set_phone PHONE_B 4messages_repeated 'BENSON' 'BFINJC' 'BLISTAC' 'BLOODRA'
end
return

:SET_4_PHONE_MESSAGES
if
    0ADC: test_cheat "PHONEA4Y"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    037D: set_phone PHONE_A 4messages 'ADMIRAL' 'AMBULAN' 'ANGEL' 'BAGGAGE'
end
if
    0ADC: test_cheat "PHONEB4Y"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    037D: set_phone PHONE_B 4messages 'BENSON' 'BFINJC' 'BLISTAC' 'BLOODRA'
end
return

:GIVE_PLAYER_DETONATOR
if
    0ADC: test_cheat "DETONATE"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    037F: give_player_detonator
end
return

:SET_5_REPEATED_PHONE_MESSAGES
if
    0ADC: test_cheat "PHONEA5X"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0386: set_phone PHONE_A 5messages_repeated 'ADMIRAL' 'AMBULAN' 'ANGEL' 'BAGGAGE' 'BANSHEE'
end
if
    0ADC: test_cheat "PHONEB5X"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0386: set_phone PHONE_B 5messages_repeated 'BENSON' 'BFINJC' 'BLISTAC' 'BLOODRA' 'BOBCAT'
end
return

:SET_5_PHONE_MESSAGES
if
    0ADC: test_cheat "PHONEA5Y"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0387: set_phone PHONE_A 5messages 'ADMIRAL' 'AMBULAN' 'ANGEL' 'BAGGAGE' 'BANSHEE'
end
if
    0ADC: test_cheat "PHONEB5Y"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0387: set_phone PHONE_B 5messages 'BENSON' 'BFINJC' 'BLISTAC' 'BLOODRA' 'BOBCAT'
end
return

:SET_6_REPEATED_PHONE_MESSAGES
if
    0ADC: test_cheat "PHONEA6X"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0388: set_phone PHONE_A 6messages_repeated 'ADMIRAL' 'AMBULAN' 'ANGEL' 'BAGGAGE' 'BANSHEE' 'BARRCKS'
end
if
    0ADC: test_cheat "PHONEB6X"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0388: set_phone PHONE_B 6messages_repeated 'BENSON' 'BFINJC' 'BLISTAC' 'BLOODRA' 'BOBCAT' 'BOXVILL'
end
return

:SET_6_PHONE_MESSAGES
if
    0ADC: test_cheat "PHONEA6Y"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0389: set_phone PHONE_A 6messages 'ADMIRAL' 'AMBULAN' 'ANGEL' 'BAGGAGE' 'BANSHEE' 'BARRCKS'
end
if
    0ADC: test_cheat "PHONEB6Y"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0389: set_phone PHONE_B 6messages 'BENSON' 'BFINJC' 'BLISTAC' 'BLOODRA' 'BOBCAT' 'BOXVILL'
end
return

:IS_CRANE_LIFTING_CAR
if
    PAGE == 1
then
    gosub @TEXT_STYLE
    if
        03A0:   crane -964.0 -1490.0 is_lifting_car SPAWN_CAR
    then
        0608: show_text_position 10.0 LINE text "03A0: Crane is lifting spawned vehicle"
    else
        0608: show_text_position 10.0 LINE text "03A0: Crane is not lifting spawned vehicle"
    end
end
return

:IS_PHONE_DISPLAYING_MESSAGE
if
    PAGE == 1
then
    gosub @TEXT_STYLE
    if
        03C2:   phone PHONE_A displaying_message
    then
        0608: show_text_position 10.0 LINE text "03C2: Phone-A is displaying message"
    else
        0608: show_text_position 10.0 LINE text "03C2: Phone-A is not displaying message"
    end
end
return

:IS_COLLISION_IN_MEMORY
if
    PAGE == 1
then
    gosub @TEXT_STYLE
    if
        03C6:   current_island == 1
    then
        0608: show_text_position 10.0 LINE text "03C6: Level 1 is in memory"
    else
        0608: show_text_position 10.0 LINE text "03C6: Level 1 is not in memory"
    end
end
return

:IS_CAR_VISIBLY_DAMAGED
if
    PAGE == 1
then
    gosub @TEXT_STYLE
    if
        00E0:   player $PLAYER_CHAR in_any_car
    then
        03C1: PLAYER_CAR = player $PLAYER_CHAR car_no_save
        if
            03C9:   car PLAYER_CAR visibly_damaged
        then
            0608: show_text_position 10.0 LINE text "03C9: Car is visibly damaged"
            return
        end
    end
    0608: show_text_position 10.0 LINE text "03C9: Car is not visibly damaged"
end
return

:ADD_BLIP_FOR_PICKUP_OLD
if
    0ADC: test_cheat "PICKBLIP"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0164: disable_marker BLIP
    03DB: create_marker_above_pickup SPAWN_PICK color 0 visibility 3 handle BLIP
end
return

:ADD_SPRITE_BLIP_FOR_PICKUP
if
    0ADC: test_cheat "PICKSPRITE"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0164: disable_marker BLIP
    03DD: create_marker_above_pickup SPAWN_PICK icon 16 handle BLIP
end
return

:IS_CHAR_IN_CONTROL
if
    PAGE == 1
then
    gosub @TEXT_STYLE
    if
        03E9:   actor SPAWN_CHAR is_in_control
    then
        0608: show_text_position 10.0 LINE text "03E9: Spawned character is in control"
    else
        0608: show_text_position 10.0 LINE text "03E9: Spawned character is not in control"
    end
end
return

:HAS_MILITARY_CRANE_COLLECTED_ALL_CARS
if
    PAGE == 1
then
    gosub @TEXT_STYLE
    if
        03EC:   ev_crane_collected_all_cars
    then
        0608: show_text_position 10.0 LINE text "03EC: Military crane has collected all cars"
    else
        0608: show_text_position 10.0 LINE text "03EC: Military crane has not collected all cars"
    end
end
return

:SET_GANG_PED_MODEL_PREFERENCE
if
    0ADC: test_cheat "PREFERENCE"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    05F9: TEMP_VAR = GANGPREF AND 0x80
    if
        TEMP_VAR == 0
    then
        05FA: GANGTHREAT = GANGPREF OR 0x80
        0410: override_gang_model 1 0
    else
        05F9: GANGTHREAT = GANGPREF AND 0xFFFFFF7F
        0410: override_gang_model 1 255
    end
end
return

:SET_GET_OUT_OF_JAIL_FREE
if
    0ADC: test_cheat "JAILFREE"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0413: set_player $PLAYER_CHAR get_out_of_jail_free 1
end
return

:IS_CAR_DOOR_CLOSED
if
    PAGE == 1
then
    gosub @TEXT_STYLE
    if
        00E0:   player $PLAYER_CHAR in_any_car
    then
        03C1: PLAYER_CAR = player $PLAYER_CHAR car_no_save
        if
            0415:   car PLAYER_CAR door 0 closed
        then
            0608: show_text_position 10.0 LINE text "0415: Car bonnet is closed"
            return
        end
    end
    0608: show_text_position 10.0 LINE text "0415: Car bonnet is not closed"
end
return

:REGISTER_KILL_FRENZY_PASSED
if
    0ADC: test_cheat "FRENZY"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    041B: increment_rampages_passed
end
return

:SET_CHAR_SAY
if
    0ADC: test_cheat "TALK"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    041C: make_actor $PLAYER_ACTOR say 0x7A // picked up money talk
end
return

:OVERRIDE_HOSPITAL_LEVEL
if
    0ADC: test_cheat "HOSPITAL1"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    041F: override_hospital 1
end
if
    0ADC: test_cheat "HOSPITAL2"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    041F: override_hospital 2
end
return

:OVERRIDE_POLICE_STATION_LEVEL
if
    0ADC: test_cheat "POLICE1"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0420: override_police_station 1
end
if
    0ADC: test_cheat "POLICE2"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0420: override_police_station 2
end
return

:FORCE_RAIN
if
    0ADC: test_cheat "FORCERAIN"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    05F9: TEMP_VAR = FORCERAIN AND 0x20
    if
        TEMP_VAR == 0
    then
        05FA: FORCERAIN = FORCERAIN OR 0x20
        0421: force_rain 1
    else
        05F9: FORCERAIN = FORCERAIN AND 0xFFFFFFDF
        0421: force_rain 0
    end
end
return

:DOES_GARAGE_CONTAIN_CAR
if
    PAGE == 1
then
    gosub @TEXT_STYLE
    if
        0422:   garage_contain_car GARAGE SPAWN_CAR
    then
        0608: show_text_position 10.0 LINE text "0422: Garage contains spawned vehicle"
    else
        0608: show_text_position 10.0 LINE text "0422: Garage does not contain spawned vehicle"
    end
end
return

:IS_THREAT_FOR_PED_TYPE
if
    PAGE == 1
then
    gosub @TEXT_STYLE
    if
        042A:   ped_threat_exists 8 1
    then
        0608: show_text_position 10.0 LINE text "042A: Ped type 8 is threat to player"
    else
        0608: show_text_position 10.0 LINE text "042A: Ped type 8 is not threat to player"
    end
end
if
    0ADC: test_cheat "GANGTHREAT"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    05F9: TEMP_VAR = GANGTHREAT AND 0x40
    if
        TEMP_VAR == 0
    then
        05FA: GANGTHREAT = GANGTHREAT OR 0x40
        03F1: pedtype 8 add_threat 1
    else
        05F9: GANGTHREAT = GANGTHREAT AND 0xFFFFFFBF
        03F2: pedgroup 8 remove_threat 1
    end
end
return

:GET_CHAR_IN_CAR_PASSENGER_SEAT
if
    0ADC: test_cheat "PASSENGER"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    if and
        8118:   not actor SPAWN_CHAR dead
        8119:   not car SPAWN_CAR wrecked
    then
        05F9: TEMP_VAR = PASSENGER AND 0x2000
        if
            TEMP_VAR == 0
        then
            05FA: PASSENGER = PASSENGER OR 0x2000
            01D4: actor SPAWN_CHAR go_to_car SPAWN_CAR and_enter_it_as_a_passenger
        else
            05F9: PASSENGER = PASSENGER AND 0xFFFFDFFF
            03E2: actor SPAWN_CHAR leave_any_car
        end
    end
end
return

:IS_PLAYER_LIFTING_A_PHONE
if
    PAGE == 1
then
    gosub @TEXT_STYLE
    if
        0447:   player $PLAYER_CHAR lifting_a_phone
    then
        0608: show_text_position 10.0 LINE text "0447: Player is lifting phone"
    else
        0608: show_text_position 10.0 LINE text "0447: Player is not lifting phone"
    end
end
return

:SET_SCRIPT_FIRE_AUDIO
if
    0ADC: test_cheat "CARFIRE"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    02D1: destroy_fire CARFIRE
    0325: CARFIRE = create_car SPAWN_CAR fire
    0444: set_fire CARFIRE audio 0
end
return

:GET_NUMBER_OF_SEATS_IN_MODEL
05F9: SEATS = SEATS AND 0xFFFFF0FF
if
    00E0:   player $PLAYER_CHAR in_any_car
then
    03C1: PLAYER_CAR = player $PLAYER_CHAR car_no_save
    0441: PLAYER_CAR = car PLAYER_CAR model
    047D: get_number_of_seats_in_model PLAYER_CAR store_to TEMP_VAR
    05FF: TEMP_VAR = TEMP_VAR SHL 8
    05FA: SEATS = SEATS OR TEMP_VAR
end
return

:IS_CHAR_IN_ANY_BOAT
if
    PAGE == 1
then
    gosub @TEXT_STYLE
    if
        04A7:   actor $PLAYER_ACTOR in_any_boat
    then
        0608: show_text_position 10.0 LINE text "04A7: Player character is in any boat"
    else
        0608: show_text_position 10.0 LINE text "04A7: Player character is not in any boat"
    end
end
return

:IS_CHAR_IN_ANY_HELI
if
    PAGE == 1
then
    gosub @TEXT_STYLE
    if
        04A9:   actor $PLAYER_ACTOR in_any_heli
    then
        0608: show_text_position 10.0 LINE text "04A9: Player character is in any heli"
    else
        0608: show_text_position 10.0 LINE text "04A9: Player character is not in any heli"
    end
end
return

:IS_CHAR_IN_ANY_PLANE
if
    PAGE == 1
then
    gosub @TEXT_STYLE
    if
        04AB:   actor $PLAYER_ACTOR in_any_plane
    then
        0608: show_text_position 10.0 LINE text "04AB: Player character is in any plane"
    else
        0608: show_text_position 10.0 LINE text "04AB: Player character is not in any plane"
    end
end
return

:IS_CHAR_IN_FLYING_VEHICLE
if
    PAGE == 1
then
    gosub @TEXT_STYLE
    if
        04C8:   actor $PLAYER_ACTOR in_flying_vehicle
    then
        0608: show_text_position 10.0 LINE text "04C8: Player character is in flying vehicle"
    else
        0608: show_text_position 10.0 LINE text "04C8: Player character is not in flying vehicle"
    end
end
return

:GET_MAX_WANTED_LEVEL
if
    0ADC: test_cheat "WANTED0"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    01F0: set_max_wanted_level_to 0
end
if
    0ADC: test_cheat "WANTED4"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    01F0: set_max_wanted_level_to 4
end
if
    0ADC: test_cheat "WANTED6"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    01F0: set_max_wanted_level_to 6
end
return

:PRINT_HELP
if
    0ADC: test_cheat "PRINTHELP"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0511: show_text_box_1number 'NUMBER' number 1
    wait 5000
    03E6: remove_text_box
    018C: play_sound 1 at 0.0 0.0 0.0
    0513: show_permanent_text_box_1number 'NUMBER' number 2
    wait 5000
    03E6: remove_text_box
end
return

:GET_PLAYER_DRUNKENNESS
052D: get_player $PLAYER_CHAR drunkenness_to TEMP_VAR
if
    0ADC: test_cheat "DRUNK"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    if
        TEMP_VAR == 0
    then
        052C: set_player $PLAYER_CHAR drunk_visuals 255
    else
        052C: set_player $PLAYER_CHAR drunk_visuals 0
    end
end
return

:ADD_LOAN_SHARK_VISITS
if
    0ADC: test_cheat "LOAN"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0530: add_loan_shark_visits 10
end
return

:ADD_MOVIE_STUNTS
if
    0ADC: test_cheat "MOVIE"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0532: add_movie_stunts 10
end
return

:ADD_GARBAGE_PICKUPS
if
    0ADC: test_cheat "GARBAGE"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0535: add_garbage_pickups 10
end
return

:SET_TOP_SHOOTING_RANGE_SCORE
if
    0ADC: test_cheat "RANGESCORE"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0537: set_top_shooting_range_score 10
end
return

:ADD_SHOOTING_RANGE_RANK
if
    0ADC: test_cheat "RANGERANK"
then
    018C: play_sound 1 at 0.0 0.0 0.0
    0538: add_shooting_range_rank 10
end
return

:IS_CHAR_TOUCHING_VEHICLE
if
    PAGE == 1
then
    gosub @TEXT_STYLE
    if
        0547:   actor $PLAYER_ACTOR touching_car SPAWN_CAR
    then
        0608: show_text_position 10.0 LINE text "0547: Player character is touching spawned boat"
    else
        0608: show_text_position 10.0 LINE text "0547: Player character is not touching spawned boat"
    end
end
return
